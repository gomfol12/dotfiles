#!/bin/zsh
# TODO: git rebase?

setopt prompt_subst
autoload -U colors && colors

### timer and exit code
TIMER_THRESHOLD=1

precmd_exit_code_and_time()
{
    last_exit_code=$?

    if [[ -n "$COMMAND_DURATION" ]] && [[ $last_exit_code -ne 0 ]]; then
        print -P "%F{red}[%F{yellow}${last_exit_code}%F{red}][%F{yellow}${COMMAND_DURATION}%F{red}]"
    elif [[ $last_exit_code -ne 0 ]]; then
        print -P "%F{red}[%F{yellow}${last_exit_code}%F{red}]"
    elif [[ -n "$COMMAND_DURATION" ]]; then
        print -P "%F{red}[%F{yellow}${COMMAND_DURATION}%F{red}]"
    fi
}

autoload -U add-zsh-hook
add-zsh-hook precmd precmd_exit_code_and_time

### vcs info
autoload -Uz vcs_info
precmd() { vcs_info }

zstyle ':vcs_info:*' enable git
zstyle ':vcs_info:*' check-for-changes true
zstyle ':vcs_info:*' stagedstr '+'
zstyle ':vcs_info:*' unstagedstr '*'

zstyle ':vcs_info:git:*' formats '%F{red}[%F{green}%b%F{red}%m%u%c]'

zstyle ':vcs_info:git*+set-message:*' hooks git-untracked git-st

# function to check for untracked files in directory
+vi-git-untracked(){
    if [[ $(git rev-parse --is-inside-work-tree 2> /dev/null) == 'true' ]] && \
        git status --porcelain | grep -q '^?? ' 2> /dev/null ; then
        # This will show the marker if there are any untracked files in repo.
        # If instead you want to show the marker only if there are untracked
        # files in $PWD, use:
        #[[ -n $(git ls-files --others --exclude-standard) ]] ; then
        hook_com[staged]+='%%'
    fi
}

# function to compare local changes to remote changes
function +vi-git-st() {
    local ahead behind
    local -a gitstatus

    # Exit early in case the worktree is on a detached HEAD
    git rev-parse ${hook_com[branch]}@{upstream} >/dev/null 2>&1 || return 0

    local -a ahead_and_behind=(
        $(git rev-list --left-right --count HEAD...${hook_com[branch]}@{upstream} 2>/dev/null)
    )

    ahead=${ahead_and_behind[1]}
    behind=${ahead_and_behind[2]}

    (( $ahead )) && gitstatus+=( "+${ahead}" )
    (( $behind )) && gitstatus+=( "-${behind}" )

    hook_com[misc]+=${(j:/:)gitstatus}
}

### Prompt
PROMPT='%F{red}[%(!.%F{red}.%F{green})%n%F{green}@%m%F{white}:%F{blue}%~%F{red}]%f'
PROMPT+='${vcs_info_msg_0_} '
